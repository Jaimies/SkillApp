plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'

    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id 'io.gitlab.arturbosch.detekt' version '1.5.0'
}

android {

    kotlinOptions.jvmTarget = '1.8'
    signingConfigs {
        release {
            storeFile = file(Signing.keyPath)
            storePassword = Signing.storePassword
            keyAlias = Signing.keyAlias
            keyPassword = Signing.keyPassword
        }
    }

    compileSdkVersion App.compileSdk
    buildToolsVersion App.buildTools

    defaultConfig {
        applicationId App.applicationId
        minSdkVersion App.minSdk
        targetSdkVersion App.targetSdk
        versionCode App.versionCode
        versionName App.versionName
        testInstrumentationRunner = 'com.jdevs.timeo.TimeoTestRunner'
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig = signingConfigs.release
        }
        debug.multiDexEnabled = true
    }

    sourceSets {
        androidTest.java.srcDirs = ['src/sharedTest/kotlin', 'src/androidTest/kotlin']
        test.java.srcDirs = ['src/sharedTest/kotlin', 'src/test/kotlin']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures.dataBinding = true
}

detekt {
    ignoreFailures = true
    autoCorrect = true

    reports {
        xml.enabled = false
        txt.enabled = false
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$Versions.kotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$Versions.coroutines"

    implementation "androidx.appcompat:appcompat:$Versions.appcompat"
    implementation "androidx.core:core-ktx:$Versions.androidxCore"
    implementation "androidx.constraintlayout:constraintlayout:$Versions.constraintLayout"
    implementation "androidx.legacy:legacy-support-v4:$Versions.legacy"

    implementation project(':domain')
    implementation project(':data')
    implementation project(':shared')
    implementation project(':lib')

    implementation "com.google.android.gms:play-services-auth:$Versions.gmsAuth"

    implementation "androidx.room:room-ktx:$Versions.room"
    implementation "com.google.firebase:firebase-firestore:$Versions.firestore"

    implementation "androidx.fragment:fragment-ktx:$Versions.fragment"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$Versions.lifecycle"

    implementation "androidx.navigation:navigation-fragment-ktx:$Versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$Versions.navigation"

    implementation "com.google.android.material:material:$Versions.material"

    implementation "com.github.PhilJay:MPAndroidChart:v$Versions.MPAndroidChart"
    implementation "com.wdullaer:materialdatetimepicker:$Versions.datepicker"
    implementation "androidx.viewpager2:viewpager2:$Versions.viewpager"
    implementation "androidx.paging:paging-runtime-ktx:$Versions.paging"

    implementation "androidx.preference:preference-ktx:$Versions.preference"
    implementation "com.jakewharton.threetenabp:threetenabp:$Versions.threeTenABP"
    testImplementation "org.threeten:threetenbp:$Versions.threeTenBP"

    // Dagger
    implementation "com.google.dagger:hilt-android:$Versions.hilt"
    kapt "com.google.dagger:hilt-android-compiler:$Versions.hilt"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$Versions.androidxHilt"
    kapt "androidx.hilt:hilt-compiler:$Versions.androidxHilt"

    kapt "com.android.databinding:compiler:$Versions.dataBinding"

    lintChecks project(':lint')

    testImplementation 'io.kotest:kotest-runner-junit5-jvm:4.1.0'
    testImplementation "org.hamcrest:hamcrest-all:$Versions.hamcrest"

    androidTestImplementation "androidx.test.ext:junit:$Versions.junitExt"
    androidTestImplementation "androidx.test:runner:$Versions.androidxTestRunner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$Versions.espresso"
    androidTestImplementation "junit:junit:$Versions.junit"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$Versions.coroutines"
    androidTestImplementation "org.mockito:mockito-core:$Versions.mockito"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:$Versions.dexMaker"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$Versions.espresso"

    // Testing code should not be included in the main code.
    // Once https://issuetracker.google.com/128612536 is fixed this can be fixed.
    debugImplementation "androidx.fragment:fragment-testing:$Versions.fragment"
    implementation "androidx.test:core:$Versions.androidxTestCore"

    // Browsing SQLite database
    // To enable, run $ adb forward tcp:8080 tcp:8080
    // and connect to http://localhost:8080
    debugImplementation "com.amitshekhar.android:debug-db:$Versions.debugDb"
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'io.gitlab.arturbosch.detekt' version '1.23.6'
    id 'com.mikepenz.aboutlibraries.plugin'
}

android {
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    signingConfigs {
        release {
            storeFile = file(Signing.keyPath)
            storePassword = Signing.storePassword
            keyAlias = Signing.keyAlias
            keyPassword = Signing.keyPassword
        }
    }

    defaultConfig {
        applicationId App.applicationId
        minSdk App.minSdk
        compileSdk App.compileSdk
        targetSdk App.targetSdk
        versionCode App.versionCode
        versionName App.versionName
        testInstrumentationRunner "com.theskillapp.skillapp.screenshots.CustomTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig = signingConfigs.release
        }
        debug {
            multiDexEnabled = true
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0']
        }
    }
    lint {
        abortOnError false
    }
    namespace 'com.theskillapp.skillapp'
}

detekt {
    ignoreFailures = true
    autoCorrect = true

    reports {
        xml.enabled = false
        txt.enabled = false
    }
}

configurations {
    all {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
    }
}

kotlin {
    jvmToolchain 17
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$Versions.kotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$Versions.coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$Versions.serialization"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation "androidx.appcompat:appcompat:$Versions.appcompat"
    implementation "androidx.core:core-ktx:$Versions.androidxCore"
    implementation "androidx.constraintlayout:constraintlayout:$Versions.constraintLayout"
    implementation "androidx.preference:preference-ktx:$Versions.preference"

    implementation project(':domain')
    implementation project(':data')
    implementation project(':shared')

    implementation "androidx.room:room-ktx:$Versions.room"

    implementation "androidx.fragment:fragment-ktx:$Versions.fragment"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$Versions.lifecycle"

    implementation "androidx.navigation:navigation-fragment-ktx:$Versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$Versions.navigation"

    implementation "com.google.android.material:material:$Versions.material"

    implementation "com.github.PhilJay:MPAndroidChart:v$Versions.MPAndroidChart"
    implementation 'com.github.AppIntro:AppIntro:6.2.0'
    implementation "androidx.paging:paging-runtime-ktx:$Versions.paging"

    implementation "androidx.work:work-runtime-ktx:$Versions.workManager"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'

    implementation "com.google.dagger:hilt-android:$Versions.hilt"
    kapt "com.google.dagger:hilt-android-compiler:$Versions.hilt"

    implementation "com.mikepenz:aboutlibraries:$Versions.aboutLibraries"

    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "io.mockk:mockk:$Versions.mockk"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$Versions.kotlin"
    testImplementation "io.kotest:kotest-runner-junit5:$Versions.kotestJUnit5"
    testImplementation "io.kotest:kotest-framework-datatest:$Versions.kotestJUnit5"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$Versions.coroutines"
    testImplementation project(":sharedTest")
    testImplementation testFixtures(project(":domain"))

    androidTestImplementation project(":sharedTest")
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation "com.google.dagger:hilt-android-testing:$Versions.hilt"
    androidTestImplementation testFixtures(project(":domain"))
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$Versions.hilt"
}

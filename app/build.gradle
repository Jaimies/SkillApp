plugins {

    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'

    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id 'io.gitlab.arturbosch.detekt' version '1.5.0'
}

android {

    kotlinOptions.jvmTarget = '1.8'
    signingConfigs {
        release {
            storeFile = file(Signing.keyPath)
            storePassword = Signing.storePassword
            keyAlias = Signing.keyAlias
            keyPassword = Signing.keyPassword
        }
    }

    compileSdkVersion App.compileSdk
    buildToolsVersion App.buildTools
    defaultConfig {

        applicationId App.applicationId
        minSdkVersion App.minSdk
        targetSdkVersion App.targetSdk
        versionCode App.versionCode
        versionName App.versionName

        testInstrumentationRunner = 'com.jdevs.timeo.TimeoTestRunner'
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig = signingConfigs.release
        }
        debug.multiDexEnabled = true
    }

    sourceSets {
        androidTest.java.srcDirs = ['src/sharedTest/kotlin', 'src/androidTest/kotlin']
        test.java.srcDirs = ['src/sharedTest/kotlin', 'src/test/kotlin']
    }

    dataBinding.enabled = true
}

detekt {
    ignoreFailures = true
    autoCorrect = true

    reports {
        xml.enabled = false
        txt.enabled = false
    }
}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$Versions.kotlin"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$Versions.coroutines"

    // Core Android libraries and legacy support
    implementation "androidx.appcompat:appcompat:$Versions.appcompat"

    // Androidx Core extensions
    implementation "androidx.core:core-ktx:$Versions.androidxCore"

    // ConstraintLayout
    implementation "androidx.constraintlayout:constraintlayout:$Versions.constraintLayout"

    // Legacy support
    implementation "androidx.legacy:legacy-support-v4:$Versions.legacy"

    // Play Auth
    implementation "com.google.android.gms:play-services-auth:$Versions.gmsAuth"

    // The domain and data layers
    implementation project(':domain')
    implementation project(':data')

    // Shared and lib modules
    implementation project(':shared')
    implementation project(':lib')

    // Room and Firebase
    implementation "androidx.room:room-ktx:$Versions.room"
    implementation "com.google.firebase:firebase-auth:$Versions.auth"
    implementation "com.google.firebase:firebase-firestore:$Versions.firestore"

    // Fragment and Lifecycle extensions
    implementation "androidx.fragment:fragment-ktx:$Versions.fragment"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$Versions.lifecycle"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$Versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$Versions.navigation"

    // Material
    implementation "com.google.android.material:material:$Versions.material"

    // MPAndroidChart library for creating awesome charts
    implementation "com.github.PhilJay:MPAndroidChart:v$Versions.MPAndroidChart"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:$Versions.paging"

    // Androidx Preference library
    implementation "androidx.preference:preference-ktx:$Versions.preference"

    // java.time.* backport for Android
    implementation "com.jakewharton.threetenabp:threetenabp:$Versions.threeTenABP"

    // For tests use default ThreeTenBP library
    testImplementation "org.threeten:threetenbp:$Versions.threeTenBP"

    // Dependency injection
    implementation "com.google.dagger:dagger:$Versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$Versions.dagger"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$Versions.dagger"

    // Data Binding
    kapt "com.android.databinding:compiler:$Versions.dataBinding"

    // Lint
    lintChecks project(':lint')

    // Test
    testImplementation "junit:junit:$Versions.junit"
    testImplementation "org.hamcrest:hamcrest-all:$Versions.hamcrest"

    // Android Test
    androidTestImplementation "androidx.test.ext:junit:$Versions.junitExt"
    androidTestImplementation "androidx.test:runner:$Versions.androidxTestRunner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$Versions.espresso"
    androidTestImplementation "junit:junit:$Versions.junit"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$Versions.coroutines"
    androidTestImplementation "org.mockito:mockito-core:$Versions.mockito"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:$Versions.dexMaker"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$Versions.espresso"

    // Testing code should not be included in the main code.
    // Once https://issuetracker.google.com/128612536 is fixed this can be fixed.

    debugImplementation "androidx.fragment:fragment-testing:$Versions.fragment"
    implementation "androidx.test:core:$Versions.androidxTestCore"

    // Browsing SQLite database
    // To enable, run $ adb forward tcp:8080 tcp:8080
    // and connect to http://localhost:8080
    debugImplementation "com.amitshekhar.android:debug-db:$Versions.debugDb"
}
